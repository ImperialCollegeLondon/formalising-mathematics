import data.real.basic
import order.filter.at_top_bot
import topology.instances.real
/-

## Sequences, revisited

Recall that in week 3 we made these definitions:

-/

local notation `|` x `|` := abs x

/-- `l` is the limit of the sequence `a` of reals -/
definition is_limit (a : â„• â†’ â„) (l : â„) : Prop :=
âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, | a n - l | < Îµ

/-

We then spent some time proving things like
if aâ‚™ â†’ l and bâ‚™ â†’ m then aâ‚™ * bâ‚™ â†’ l * m.

Let's see another, much shorter, proof of these things using filters,
and of course also using facts from `mathlib` about filters.

-/

open filter

open_locale topological_space

open metric

theorem is_limit_iff_tendsto (a : â„• â†’ â„) (l : â„) :
is_limit a l â†” tendsto a at_top (ð“ l) :=
begin
  rw metric.tendsto_at_top,
  refl,
end

theorem is_limit_mul (a b : â„• â†’ â„) (l m : â„)
  (ha : is_limit a l) (hb : is_limit b m) :
  is_limit (a * b) (l * m) :=
begin
  rw is_limit_iff_tendsto at *,
  exact tendsto.mul ha hb,
end

/-

This was much less painful than what we went through in week 3! So where
did the work go?

The next 130 lines of this file discuss the first proof, namely
`is_limit_iff_tendsto`. Clearly the key ingredients is
`metric.tendsto_at_top`. There are no exercises here, I will just
explain what's going on, and talk about definitions (e.g. `is_limit`)
and their cost.

The second proof uses `is_limit_iff_tendsto` to reduce `is_limit_mul`
to a theorem about filters, and them proves it with `tendsto.mul`. We will
prove our own version of `tendsto.mul` in this file. So if you want to
get on with the proving you can skip straight down to
the `## tendsto.mul` section on line 184 or so.

The first proof 

## Definitions in Lean

Each *definition* you make in Lean comes with a cost. For example
check out Lean's definition of `finset`, the type of finite sets.
Right click on `finset` below and click on "go to definition".
You see one definition, and then over 2000 lines of theorems
about this definition. Don't forget to close the file afterwards!
-/

#check finset

/-
The theorems are necessary because it's no good just defining some
concept of a finite set, you need to make it intuitive to use for the
end user, so you need to prove that a subset of a finite set is finite,
the union of two finite sets is finite, the image of a finite set under
a map is finite, the product of two finite sets is finite, a finite product
of finite sets indexed by a finite set is finite, etc etc. Every one of those
lemmas in that file is completely obvious to a mathematician, but needs
to be proved in Lean so that mathematicians can use finite sets the
way they intuitively want to. See if you can understand some of the
statements proved about finite sets in that file. Be very careful
not to edit it though! If you do accidentally change it, just close the
file without saving, or use ctrl-Z to undo your changes. 

When we developed the theory of limits of sequences in week 3, we 
made the definition `is_limit`. This definition comes with a cost;
to make it useful to the end user, we need to prove a ton of theorems
about `is_limit`. This is what happens in an undergraduate analysis
class -- you see the definition, and then you make what computer
scientists call the "API" or the "interface" -- a bunch of lemmas
and theorems about `is_limit`, for example `is_limit_add`, which says
that `aâ‚™ â†’ l` and `bâ‚™ â†’ m` implies `a_n + b_n â†’ l + m`, and also `is_limit_neg`,
`is_limit_sub`, `is_limit_mul` and so on.

But it turns out that `is_limit` is just a very special case of `tendsto`,
and because `tendsto` is already in mathlib, there is already a very big
API for `tendsto` which has developed organically over the last few
years. It was started by the original writer of `tendsto` and then
it grew as other people used `tendsto` more, and added to the list of useful
lemmas as they used `tendsto` to do other things and then abstracted out
properties which they discovered were useful. For example, this week
(I write this in Feb 2021) Heather Macbeth was working on modular forms in Lean
and she discovered that she needed a lemma about `tendsto`, which, after some
discussion on the Zulip Lean chat, Heather and I realised was a statement
about how `tendsto` commutes with a certain kind of coproduct. We proved this
lemma, Heather is right now in the process of adding it (`tendsto.prod_map_coprod`)
to `mathlib`, Lean's maths library.

https://github.com/leanprover-community/mathlib/pull/6372

I will remark that I would never have worked on that problem with Heather
if it hadn't been for the fact that I'd been teaching you about filters
and hence I had to learn about them properly!

Let's take a look at our new proof of `tendsto_mul` again. The proof follows
from two 2-line lemmas. I will talk you through the first one, and you can
experiment with the second one. Let's take a look at the first one.

-/

example (a : â„• â†’ â„) (l : â„) :
is_limit a l â†” tendsto a at_top (ð“ l) :=
begin
  rw metric.tendsto_at_top,
  refl,
end

/-

The guts of the first one is `metric.tendsto_at_top`, which is actually
a statement about metric spaces. It says that in any metric space,
the standard metric space epsilon-N definition of a limit of a sequence
is a special case of this filter `tendsto` predicate. Here is a proof
with more details spelt out (`simp_rw` is just a slightly more powerful
version of `rw` which we need for technical reasons here, because `rw` will
not see under a `âˆ€` statement -- it will not "work under binders"):
-/

example (a : â„• â†’ â„) (l : â„) :
is_limit a l â†” tendsto a at_top (ð“ l) :=
begin
  simp_rw [metric.tendsto_nhds, eventually_iff, mem_at_top_sets],
  refl,
end

/-

This more explicit proof uses the following fancy notation
called "filter.eventually" :

`(âˆ€á¶  (x : Î±) in F, P x) â†” {x : Î± | P x} âˆˆ F` (true by definition, or
you can `rw eventually_iff`)

and then it just boils down to the following two mathematical facts
(here `ball l Îµ` is the open ball radius `Îµ` centre `l` ),
the first being `metric.tendsto_nhds` and the second `mem_at_top_sets`:

1) If `a` is in a metric space, then `S âˆˆ ð“ l â†” âˆƒ Îµ > 0, ball l Îµ âŠ† S`
2) If `at_top` is the filter on on `â„•` that we saw last time then
`T âˆˆ at_top â†” âˆƒ N : â„•, {n : â„• | N â‰¤ n} âŠ† T`

After that it's easy, because `tendsto a at_top (ð“ l)` then means,
by definition of `tendsto`, 

`âˆ€ S : set â„, S âˆˆ ð“ l â†’ a â»Â¹' S âˆˆ at_top`

which translates into

`âˆ€ S : set â„, (âˆƒ Îµ > 0, ball l Îµ âŠ† S) â†’ (âˆƒ N, n â‰¥ N â†’ a n âˆˆ S)`

and if you unfold the logical packaging you will see that this is just
the usual definition of `is_limit` (note that `a n âˆˆ ball l Îµ` is
definitionally equal to `dist (a n) l < Îµ` which, for the reals, is
definitionally equal to `|a n - l| < Îµ`).

## tendsto.mul

Now let's look at the second example.

-/

example (a b : â„• â†’ â„) (l m : â„) (ha : is_limit a l) (hb : is_limit b m) :
  is_limit (a * b) (l * m) :=
begin
  rw is_limit_iff_tendsto at *,
  exact tendsto.mul ha hb,
end

/-

If you hover over `tendsto.mul` in that proof, you will perhaps be able to make
out that it says the following: if we have a topological space `M` with a
continuous multiplication on it, and if `F` is a filter on `Î±` and `f` and `g`
are maps `Î± â†’ M`, then `tendsto f F (ð“ l)` and `tendsto g F (ð“ m)` implies
`tendsto (f * g) F ð“ (l * m)`. We apply this with `F` the cofinite filter
and we're done, at least modulo the assertion that multiplication
on â„ is a continuous function. How did Lean know this? Well, 
`[has_continuous_mul M]` was in square brackets so that means that
the type class inference system is supposed to deal with it. Let's
see how it gets on with the assertion that multiplication is continuous
on the reals.

-/

-- multiplication is continuous on the reals.
example : has_continuous_mul â„ :=
begin
  -- Ask the type class inference system whether it knows this
  apply_instance
end
-- It does!

/-

The people who defined `â„` in Lean made a definition, and the price they
had to then pay for making it usable was that they had to make a big API for
`â„`, proving stuff like a non-empty bounded set of reals has a least
upper bound, and that the reals were a topological ring (and hence
multiplication was continuous). But this price was paid way back in 2018
so we mathematicians can now use these facts for free.

All that remains then, if we want to see the details, is to
*prove* `tendsto.mul`, and this is a statement about filters on topological
spaces, so let's do it. First -- what does `continuous` mean?

## Continuity

Let `X` and `Y` be topological spaces, and say `f : X â†’ Y` is a function.

-/
variables (X Y : Type) [topological_space X] [topological_space Y] (f : X â†’ Y)

/-

If `x : X`, then what does it mean for `f` to be continuous at `x`?
Intuitively, it means that if you move `x` by a small amount, then `f x`
moves by a small amount. In other words, `f` sends a small neighbourhood
of `x` into a small neighbourhood of `f x`. 

If our mental model of the neighbourhood filter `ð“ x` is some kind of
generalised set corresponding to an infinitesimally small
neighbourhood of `x`, you will see why Lean makes the following
definition of `continuous_at`:

-/

lemma continuous_at_def (x : X) :
  continuous_at f x â†” tendsto f (ð“ x) (ð“ (f x)) :=
begin
  -- true by definition
  refl
end

/-

Out of interest, you were probably told the definition of what it means
for a function `f : X â†’ Y` between *metric* spaces to be continuous at `x`.
Were you ever told what it means for a function between *topological* spaces
to be continuous at `x`, rather than just continuous on all of `X`? This
is what it means.

Now let's start on the proof of `tendsto.mul`, by building an API
for the `continuous_at` definition. Don't forget things like

`tendsto_id : tendsto id x x`
`tendsto.comp : tendsto g G H â†’ tendsto f F G â†’ tendsto (g âˆ˜ f) F H`

from Part A.
-/

-- this first lemma called `continuous_at_id`. Prove it yourself using
-- facts from Part A.
example (x : X) : continuous_at id x :=
begin
  sorry
end

-- recall we have `f : X â†’ Y`. Now let's add in a `Z`.
variables (Z : Type) [topological_space Z] (g : Y â†’ Z)

-- this is called `continuous_at.comp`. Prove it yourself using
-- facts from Part A.
example (x : X) (hf : continuous_at f x) (hg : continuous_at g (f x)) :
continuous_at (g âˆ˜ f) x :=
begin
  sorry
end

/-

Now we prove a key result, called `tendsto.prod_mk_nhds`. Notation for product
of types: if `Y` and `Z` are types then `Y Ã— Z` is the product type, and 
the notation for a general term is `(y, z) : Y Ã— Z` with `y : Y` and `z : Z`.

A special case of the theorem below is that if `f : X â†’ Y` and `g : X â†’ Z` are
continuous at `x` then the product map `f Ã— g : X â†’ Y Ã— Z` is also continuous
at `x`. We will actually prove something more general -- if `Î±` is any type
and `F : filter Î±` is any filter and if `y : Y` and `z : Z` and if
`f : Î± â†’ Y` and `g : Î± â†’ Z` satisfy `tendsto f F (ð“ y)` and `tendsto g F (ð“ z)`,
then `tendsto (f Ã— g) F (ð“ (y,z))`, where `f Ã— g` is the map `Î» x, (f x, g x)`.
The key fact you will need from the product topology API is 
`mem_nhds_prod_iff : S âˆˆ ð“ ((a, b) : X Ã— Y) â†”`
  `âˆƒ (U : set X) (H : U âˆˆ ð“ a) (V : set Y) (H : V âˆˆ ð“ b), U.prod V âŠ† S`
This is all you should need about the product topology (we won't go into how
the product topology is defined, but the key fact mathematically says that a
neighbourhood of `(a,b) : X Ã— Y` contains a product of neighbourhoods of `X` and of `Y`).

You will also need to know

`mk_mem_prod : a âˆˆ U â†’ b âˆˆ V â†’ (a, b) âˆˆ U.prod V`

where for `U : set X` and `V : set Y`, `U.prod V = prod U V` is the 
obvious subset of `X Ã— Y`. 

Recall also from Part A:
`mem_map : S âˆˆ map Ï† F â†” {x : Î± | Ï† x âˆˆ S} âˆˆ F`
`tendsto_def : tendsto f F G â†” âˆ€ (S : set Y), S âˆˆ G â†’ f â»Â¹' S âˆˆ F`
(although there is a gotcha here : the actual definition of 
`tendsto f F G` is `âˆ€ {S : set Y}, S âˆˆ G ...` )
-/

-- this is called `tendsto.prod_mk_nhds` in Lean but try proving it yourself.
example {Î± : Type} (f : Î± â†’ Y) (g : Î± â†’ Z) (x : X) (F : filter Î±) (y : Y) (z : Z)
  (hf : tendsto f F (ð“ y)) (hg : tendsto g F (ð“ z)) :
  tendsto (Î» x, (f x, g x)) F (ð“ (y, z)) :=
begin
  sorry,
end

/- Armed with `tendsto.prod_mk_nhds`, let's prove the version of `tendsto.mul`
 which we need. I would recommend starting with
```
set f1 : M Ã— M â†’ M := Î» mn, mn.1 * mn.2 with hf1,
set f2 : Î± â†’ M Ã— M := Î» x, (f x, g x) with hf2,
have h1 : f1 âˆ˜ f2 = f * g,
...
```
because it's `f1` and `f2` that we've been proving theorems about,
and then you can use `tendsto.comp`. 
-/

lemma key_lemma {Î± M : Type} [topological_space M] [has_mul M] 
  {f g : Î± â†’ M} {F : filter Î±} {a b : M} (hf : tendsto f F (ð“ a))
  (hg : tendsto g F (ð“ b))
  (hcontinuous : continuous_at (Î» (mn : M Ã— M), mn.1 * mn.2) (a,b)) :
  tendsto (f * g) F (ð“ (a * b)) :=
begin
  sorry
end

-- The final ingredient is that multiplication is continuous on â„, which we
-- just take from the real API:
lemma real.continuous_mul_at (a b : â„) :
  continuous_at (Î» xy : â„ Ã— â„, xy.1 * xy.2) (a, b) :=
begin
  -- it's in the library
  exact continuous.continuous_at real.continuous_mul,
end

-- and now we have all the ingredients we need for our own proof of `is_limit_mul`!
example  (a b : â„• â†’ â„) (l m : â„)
  (ha : is_limit a l) (hb : is_limit b m) :
  is_limit (a * b) (l * m) :=
begin
  rw is_limit_iff_tendsto at *,
  apply key_lemma ha hb,
  apply real.continuous_mul_at,
end

/-

You might think that this new proof "feels longer". But what you have to
understand is that it's shorter in practice, because the user doesn't
have to write `tendsto.mul` themselves, it's already there in the library.
Writing APIs is an extensive process. But using them is easy. 
-/